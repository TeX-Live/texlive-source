dnl Process this file with autoconf to produce a configure script.
dnl
dnl Kpathsea package version:
dnl   Starting development:
dnl     kpse_devel_version = dev;
dnl   Making releases:
dnl     kpse_micro_version += 1;
dnl     kpse_devel_version = ;
dnl
dnl Kpathsea shared library version:
dnl   Only bug fixes:
dnl     kpse_lib_micro += 1;
dnl   Adding new interfaces (backwards compatible)
dnl     kpse_lib_minor += 1;
dnl     kpse_lib_micro = 0;
dnl   Modifying or revoking interfaces (not backwards compatible)
dnl     kpse_lib_major += 1;
dnl     kpse_lib_minor = 0;
dnl     kpse_lib_micro = 0;
dnl
m4_define([kpse_major_version], [3])[]dnl
m4_define([kpse_minor_version], [5])[]dnl
m4_define([kpse_micro_version], [7])[]dnl
m4_define([kpse_devel_version], [])[]dnl
m4_define([kpse_version], [kpse_major_version.kpse_minor_version.kpse_micro_version[]kpse_devel_version])[]dnl

m4_define([kpse_lib_major], [4])[]dnl
m4_define([kpse_lib_minor], [1])[]dnl
m4_define([kpse_lib_micro], [0])[]dnl
m4_define([kpse_lt_version],[m4_eval(kpse_lib_major + kpse_lib_minor):kpse_lib_micro:kpse_lib_minor])[]dnl

AC_INIT([kpathsea], [kpse_version], [tex-k@mail.tug.org])
AC_CONFIG_SRCDIR([pathsearch.c])

AC_SUBST([KPSEVERSION], [kpse_version])
AC_DEFINE([KPSEVERSION], ["kpathsea version kpse_version"])
AC_SUBST([KPSE_LT_VERS], [kpse_lt_version])

dnl Write output here, instead of putting a zillion -D's on the command line.
AC_CONFIG_HEADERS([c-auto.h:c-auto.in],
 [sed 's/^#define PACKAGE_/#define KPSE_PACKAGE_/' c-auto.h >c-auto.tmp && mv -f c-auto.tmp c-auto.h
  date >stamp-auto])

sinclude(common.ac)

AH_TOP([/* c-auto.h: defines for kpathsea, as determined by configure.

   Copyright 1994-97, 2008 Karl Berry.
   Copyright 1997-99, 2002, 2005 Olaf Weber.

   This library is free software; you can redistribute it and/or
   modify it under the terms of the GNU Lesser General Public
   License as published by the Free Software Foundation; either
   version 2.1 of the License, or (at your option) any later version.

   This library is distributed in the hope that it will be useful,
   but WITHOUT ANY WARRANTY; without even the implied warranty of
   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
   Lesser General Public License for more details.

   You should have received a copy of the GNU Lesser General Public License
   along with this library; if not, see <http://www.gnu.org/licenses/>.  */

/* Guard against double inclusion. */
#ifndef KPATHSEA_C_AUTO_H
#define KPATHSEA_C_AUTO_H

/* kpathsea: the version string. */
#define KPSEVERSION "REPLACE-WITH-KPSEVERSION"])
AH_BOTTOM([#endif /* !KPATHSEA_C_AUTO_H */])

KPSE_CONFIG_FILES([Makefile])
AC_OUTPUT
