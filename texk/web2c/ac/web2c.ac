## texk/web2c/ac/web2c.ac: configure.ac fragment for the TeX Live subdirectory texk/web2c/
dnl
dnl Copyright (C) 2009-2012 Peter Breitenlohner <tex-live@tug.org>
dnl You may freely use, modify and/or distribute this file.
dnl
## configure options for TeX and MF
m4_define_default([kpse_indent_26], [26])[]dnl
AC_ARG_WITH([banner-add],
            AS_HELP_STRING([--with-banner-add=STR],
                           [add STR to version string appended to banner lines]))[]dnl
AC_ARG_WITH([editor],
            AS_HELP_STRING([--with-editor=CMD],
                           [invoke CMD from the `e' option @<:@vi +%d '%s'@:>@
                            or @<:@texworks --position=%d "%s"@:>@]))[]dnl
AC_ARG_ENABLE([auto-core],
              AS_HELP_STRING([--enable-auto-core],
                             [cause TeX&MF to dump core, given a certain filename],
                             kpse_indent_26))[]dnl
AC_ARG_ENABLE([dump-share],
              AS_HELP_STRING([--disable-dump-share],
                             [make fmt/base/mem files architecture-dependent],
                             kpse_indent_26))[]dnl
## configure options for TeX
AC_ARG_ENABLE([ipc],
              AS_HELP_STRING([--disable-ipc],
                             [disable TeX's --ipc option, i.e., pipe to a program],
                             kpse_indent_26))[]dnl
AC_ARG_ENABLE([omfonts],
              AS_HELP_STRING([--disable-omfonts],
                             [do not compile and install omfonts (use Web2C versions)],
                             kpse_indent_26))[]dnl
dnl [PROG, BUILD-OR-NO, SYNC-OR-NO, TEXT, REQUIRED-LIBS]
m4_define([kpse_tex_progs], [dnl
[[tex],    [yes], [no],  [TeX],     []],
[[etex],   [no],  [yes], [e-TeX],   []],
[[ptex],   [yes], [yes], [pTeX],    [ptexenc]],
[[eptex],  [yes], [yes], [e-pTeX],  [ptexenc]],
[[uptex],  [yes], [yes], [upTeX],   [ptexenc]],
[[euptex], [yes], [yes], [e-upTeX], [ptexenc]],
[[aleph],  [yes], [],    [Aleph],   []],
[[pdftex], [yes], [yes], [pdfTeX],  [xpdf libpng]],
[[luatex], [yes], [],    [luaTeX],  [poppler libpng zziplib]],
[[xetex],  [yes], [yes], [XeTeX],   [poppler libpng freetype2 icu-xetex teckit graphite]],
])[]dnl
m4_foreach([Kpse_Prog], [kpse_tex_progs],
           [m4_ifset([Kpse_Prog],
                     [KPSE_WITH_XTEX(Kpse_Prog)])])[]dnl
## configure options for MF
AC_ARG_ENABLE([mf],
              AS_HELP_STRING([--disable-mf],
                             [do not build METAFONT],
                             kpse_indent_26))[]dnl
AC_ARG_ENABLE([mf-nowin],
              AS_HELP_STRING([--disable-mf-nowin],
                             [do not build a separate non-windows-capable METAFONT],
                             m4_eval(kpse_indent_26+2)))[]dnl
AC_ARG_WITH([mf-x-toolkit],
            AS_HELP_STRING([--without-mf-x-toolkit],
                           [do not use X toolkit for METAFONT]))[]dnl
m4_define([kpse_mf_win], [dnl
[[epsf],      [EPSF],      [EPSF pseudo]],
[[hp2627],    [HP2627],    [HP 2627]],
[[mftalk],    [MFTALK],    [mftalk (generic server)]],
[[next],      [NEXT],      [NeXT]],
[[regis],     [REGIS],     [Regis]],
[[suntools],  [SUN],       [old Suntools (not X)]],
[[tektronix], [TEKTRONIX], [Tektronix]],
[[uniterm],   [UNITERM],   [Uniterm]],
])[]dnl
m4_foreach([Kpse_Win], [kpse_mf_win],
           [m4_ifset([Kpse_Win],
                     [KPSE_WITH_MFWIN(Kpse_Win)])])[]dnl
## configure options for MP
AC_ARG_ENABLE([mp],
              AS_HELP_STRING([--disable-mp],
                             [do not build METAPOST],
                             kpse_indent_26))[]dnl
## configure options for WEB programs
AC_ARG_ENABLE([web-progs],
              AS_HELP_STRING([--disable-web-progs],
                             [do not build WEB programs bibtex ... weave],
                             kpse_indent_26))[]dnl
